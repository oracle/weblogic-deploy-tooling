def versions_to_verify = []
def failed_versions = []
def build_failed_email_body = []

def computeVerifyVersions(String existingFiles) {
    def fileList = existingFiles.readLines()
    def exclusionList = ['10.3.6.0', '12.1.1.0', '12.1.2.0.0', '12.1.3.0.0']
    def versionList = []
    for (file in fileList) {
        def matcher = file =~ /generatedOnline-(\d+(\.\d+){3,5}).json/
        def fileVersion = matcher[0][1]
        if (exclusionList.contains(fileVersion)) {
            println('Excluding version ' + fileVersion)
        } else {
            println('Adding version ' + fileVersion)
            versionList.add(fileVersion)
        }
    }
    versions_to_verify = versionList
    print('versions_to_verify = ' + versions_to_verify)
}

def getComputedVersions() {
    return versions_to_verify
}

def addFailedEmailBody(String version, String jobName, int jobNumber, String jobUrl) {
    if (!build_failed_email_body.isEmpty()) {
        build_failed_email_body.add('')
    }
    build_failed_email_body.add(version + ' Verification failed: ' + jobName + " build: " + jobNumber)
    build_failed_email_body.add('\tView the log at: ' + jobUrl)
}

def processChildJobResult(version, jobResponse) {
    if (jobResponse.getResult() != 'SUCCESS') {
        failed_versions.add(version)
        def buildJobName = jobResponse.getDisplayName()
        def buildJobNumber = jobResponse.getNumber()
        def buildJobURL = jobResponse.getAbsoluteUrl()
        print(version + ' failed verification')
        addFailedEmailBody(version, buildJobName, buildJobNumber, buildJobURL)
    } else {
        print(version + ' passed verification')
    }
}

def getNumberOfFailures() {
    return failed_versions.size()
}

def getFailedEmailBody() {
    return String.join("\n", build_failed_email_body)
}

pipeline {
    agent any

    environment {
        oci_cli_exe = '/usr/bin/oci'
        oci_profile_name = 'WDTBUILD'
        ocir_repo_name = 'wdt/jenkins-alias-test-slave'
        ocir_compartment_id = 'ocid1.tenancy.oc1..aaaaaaaahmcbb5mp2h6toh4vj7ax526xtmihrneoumyat557rvlolsx63imq'

        oci_os_namespace_name = "${env.WDT_ALIAS_TEST_TENANCY}"
        oci_os_bucket_name = 'wdt-alias-test-files'
        oci_os_prefix = 'generatedOnline'

        downstream_job_name = 'wdt-alias-test-verify-child'
    }
    stages {
        stage ('Environment') {
            tools {
                maven 'maven-3.6.0'
                jdk 'jdk8'
            }
            steps {
                sh 'env|sort'
            }
        }
        stage ('Compute Versions') {
            environment {
                oci_files = sh(script: "${oci_cli_exe} os object list --profile ${oci_profile_name} --bucket-name ${oci_os_bucket_name} --namespace ${oci_os_namespace_name} --prefix ${oci_os_prefix} | jq -r '.data[].name'", returnStdout: true).trim()
            }
            steps {
                computeVerifyVersions("${oci_files}")
            }
        }
        stage ('Build') {
            tools {
                maven 'maven-3.6.0'
                jdk 'jdk8'
            }
            steps {
                sh 'mvn clean install -DskipTests'
                sh "cp ${WORKSPACE}/installer/target/weblogic-deploy.zip ${WORKSPACE}/"
                archiveArtifacts 'weblogic-deploy.zip'
            }
        }
        stage ('Call Child Process') {
            steps {
                script {
                    def versions = getComputedVersions()
                    echo "computed versions = ${versions}"
                    for (String version in versions) {
                        echo "Calling ${downstream_job_name} for WebLogic Server version ${version}"
                        def jobResponse = build job: "${downstream_job_name}", propagate: false, parameters: [
                            string(name: "parent_git_commit", value: "${GIT_COMMIT}"),
                            string(name: "parent_job_name", value: "${JOB_NAME}"),
                            string(name: "parent_job_number", value: "${BUILD_NUMBER}"),
                            string(name: "wls_version", value: "${version}")
                        ]
                        processChildJobResult(version, jobResponse)
                    }
                    /*
                    def failures = getNumberOfFailures()
                    if (failures > 0) {
                        error("${failures} version(s) failed verification")
                    }
                    */
                }
            }
        }
    }
    post {
        failure {
            echo "mail to address is ${WDT_BUILD_EMAIL}"
            mail to: "${WDT_BUILD_EMAIL}", from: 'noreply@oracle.com',
                 subject: "WDT: ${env.JOB_NAME} - Failed",
                 body: "${getFailedEmailBody()}"
        }
    }
}
